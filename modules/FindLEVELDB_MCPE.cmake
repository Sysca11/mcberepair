find_path(LEVELDB_MCPE_INCLUDE_DIR
	NAMES "leveldb/zlib_compressor.h"
	DOC "Include directory for LevelDB-MCPE"
)

find_library(LEVELDB_MCPE_LIBRARY
	NAMES leveldb
	PATH_SUFFIXES out-shared out-static
	DOC "Library for LevelDB-MCPE"
)

find_package(Threads REQUIRED)
find_package(ZLIB REQUIRED)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(LEVELDB_MCPE DEFAULT_MSG LEVELDB_MCPE_INCLUDE_DIR LEVELDB_MCPE_LIBRARY)

if(LEVELDB_MCPE_FOUND)
	if(NOT TARGET LEVELDB_MCPE::LEVELDB_MCPE)
		add_library(LEVELDB_MCPE::LEVELDB_MCPE UNKNOWN IMPORTED)
		target_link_libraries(LEVELDB_MCPE::LEVELDB_MCPE INTERFACE Threads::Threads)
		target_link_libraries(LEVELDB_MCPE::LEVELDB_MCPE INTERFACE ZLIB::ZLIB)

      	set_target_properties(LEVELDB_MCPE::LEVELDB_MCPE PROPERTIES
        	INTERFACE_INCLUDE_DIRECTORIES "${LEVELDB_MCPE_INCLUDE_DIR}")
		set_property(TARGET LEVELDB_MCPE::LEVELDB_MCPE APPEND PROPERTY
          IMPORTED_LOCATION "${LEVELDB_MCPE_LIBRARY}")
		set_property(TARGET LEVELDB_MCPE::LEVELDB_MCPE APPEND PROPERTY
			INTERFACE_COMPILE_FEATURES cxx_std_11)
		set_property(TARGET LEVELDB_MCPE::LEVELDB_MCPE APPEND PROPERTY
			INTERFACE_COMPILE_DEFINITIONS DLLX=)

	endif()
endif()
